import processing.sound.*;  // Importa la librería de sonido

// Variables globales
private Menu menu;
private MaquinaDeEstado maquinaDeEstado;
private SoundFile startMenuSound;  // Variable para el sonido
private SoundFile gameMusic;
private SoundFile jumpSound;  // Variable para el sonido del salto
private Parallax parallax;

// Variables para el jugador
private PImage spriteSheet;
private int spriteWidth = 252 / 6;  // Ancho de un sprite (6 columnas)
private int spriteHeight = 184 / 4; // Alto de un sprite (4 filas)
private Player sonic;  // Instancia del jugador
private Camera camera; // Instancia de la cámara
private PImage endGameImage;
// Variable para controlar si la canción se ha reproducido
private boolean endGameSoundPlayed = false;

//HUD
private HUD hud;

// Instancia de la clase Escenario
private Escenario escenario;

// Debug
private Debug debug;

void setup() {
  size(800, 600);  // Tamaño de la ventana
  String[] fondoArchivos = {"fondo/fondo0.png", "fondo/fondo1.png", "fondo/fondo2.png"};
  
  // Cargar el spriteSheet de Sonic
  spriteSheet = loadImage("sonic/sonic-10.png");

  // Crear objeto Parallax
  parallax = new Parallax(fondoArchivos, 1, 1.5, 0, -150, 16);
  
  // Cargar la imagen de fin de juego
  endGameImage = loadImage("endgame.png");

  // Cargar la fuente y crear el menú
  PFont pixelFont = createFont("Font/NiseSegaSonic.ttf", 32);
  menu = new Menu(40, 150, 100, pixelFont);

  sonic = new Player(100, height - spriteHeight, spriteWidth, spriteHeight);

  // Crear la máquina de estados
  maquinaDeEstado = new MaquinaDeEstado(menu);

  // Cargar y reproducir el sonido de fondo
  startMenuSound = new SoundFile(this, "data/sound/startMenu.mp3");
  startMenuSound.loop();  // Reproduce el sonido en bucle
  
  gameMusic = new SoundFile(this, "data/sound/Sonic_The_Hedgehog.mp3");
  jumpSound = new SoundFile(this, "data/sound/jump.wav");


  // Crear la cámara
  camera = new Camera();
  
  debug = new Debug();
  
  // Configurar el sprite sheet y sus dimensiones en la clase Debug
  debug.setSpriteSheet(spriteSheet);
  debug.setSpriteDimensions(spriteWidth, spriteHeight);
  
  // Establecer las posiciones manuales de los rectángulos
  debug.setSpriteRectPosition(570, 25);  // Ajusta la posición del rectángulo rojo
  
  //HUD
  hud = new HUD(sonic);
  
  // Crear una instancia de la clase Escenario
  escenario = new Escenario("escenario/scene0.png");  // Cargar la imagen del fondo
  // Establecer una posición manual del escenario
  escenario.setPosicionX(100);  // Desplaza el fondo 100 píxeles a la derecha
  escenario.setPosicionY(55);   // Desplaza el fondo 50 píxeles hacia abajo
}

void draw() {
  background(0);  // Limpia el fondo

  // Dibujar el parallax siempre
  parallax.dibujar();

  // Hacer que la cámara siga a Sonic
  camera.follow(sonic);
  // Aplicar la transformación de la cámara
  pushMatrix();
  camera.apply();

  // Verificar si estamos en el estado ESCENARIO
  if (maquinaDeEstado.getEstado() == MaquinaDeEstado.ESCENARIO) {
    if (!gameMusic.isPlaying()) {
      startMenuSound.stop();  // Detener música del menú
      gameMusic.loop();       // Reproducir música del juego en bucle
    }
    escenario.dibujar();
    sonic.update();  // Actualiza el estado y la posición del jugador
    sonic.display();  // Muestra al jugador en la pantalla

    // Dibujar el suelo (solo visual, no interactúa con la lógica de colisiones)
    stroke(0);
    line(-width, height, width * 2, height);
  } 
  // Si estamos en el estado ENDGAME, reproducimos la música una sola vez
  else if (maquinaDeEstado.getEstado() == MaquinaDeEstado.ENDGAME) {
    // Si no se ha reproducido la canción aún
    if (!endGameSoundPlayed) {
  gameMusic.stop();  // Detener música de fondo
  SoundFile endGameMusic = new SoundFile(this, "data/sound/gameover.mp3");
  endGameMusic.play(); // Reproducir sonido de Game Over
  endGameSoundPlayed = true;  // Marcar como reproducido
}

    // Mostrar imagen de fin de juego
    background(0); // Fondo negro para mayor contraste
    if (endGameImage != null) {
      imageMode(CENTER);
      image(endGameImage, width / 2, height / 2 - 50);
    }

    // Mostrar el texto
    textAlign(CENTER, CENTER);
    fill(255);
    textSize(20);
    text("Pulsa ESC para salir", width / 2, height / 2 + 150);
  }

  popMatrix();  // Vuelve a la matriz original para no afectar el resto
  
  // Dibujar HUD
  hud.display();
  
  // Actualizar la máquina de estados
  maquinaDeEstado.update();
  
  // Mostrar la información de depuración
  debug.display(sonic, camera);  // Este método ahora maneja la depuración visual para Sonic y la cámara
}


void keyPressed() {
  maquinaDeEstado.keyPressed(key);  // Maneja la tecla presionada
  if (key == 'a') sonic.move(-1);   // Mover a la izquierda
  if (key == 'd') sonic.move(1);    // Mover a la derecha
  if (key == ' ') sonic.jump();     // Salto
  if (key == 'q') debug.toggleDebug(sonic);  // Alternar la depuración
  // Simulación de recoger una vida extra
  if (key == 'r') { // Presiona 'r' para ganar una vida
  sonic.ganarVida();
  }
// Cambiar a estado ENDGAME con 'L'
if (maquinaDeEstado.getEstado() == MaquinaDeEstado.ESCENARIO && (key == 'l' || key == 'L')) {
    maquinaDeEstado.setEstado(MaquinaDeEstado.ENDGAME);
}



  
  // Salir del juego al presionar ESC en estado END_GAME
  if (maquinaDeEstado.getEstado() == MaquinaDeEstado.ENDGAME && key == ESC) {
    exit();
  }
}

void keyReleased() {
  if (key == 'a' || key == 'd') sonic.stop();  // Detener movimiento al soltar la tecla
}
